# _ADD_BR([name])
#
# Expands into the proper ovs-vsctl commands to create a bridge with the
# appropriate type and properties
m4_define([_ADD_BR], [[add-br $1 -- set Bridge $1 protocols=OpenFlow10,OpenFlow11,OpenFlow12,OpenFlow13,OpenFlow14,OpenFlow15 fail-mode=secure ]])

# OVS_TRAFFIC_VSWITCHD_START([vsctl-args], [vsctl-output], [=override])
#
# Creates a database and starts ovsdb-server, starts ovs-vswitchd
# connected to that database, calls ovs-vsctl to create a bridge named
# br0 with predictable settings, passing 'vsctl-args' as additional
# commands to ovs-vsctl.  If 'vsctl-args' causes ovs-vsctl to provide
# output (e.g. because it includes "create" commands) then 'vsctl-output'
# specifies the expected output after filtering through uuidfilt.
#
# Best-effort loading of all available vport modules is performed.
#
m4_define([OVS_TRAFFIC_VSWITCHD_START],
  [AT_CHECK([modprobe openvswitch])
   on_exit 'modprobe -r openvswitch'
   m4_foreach([mod], [[vport_geneve], [vport_gre], [vport_lisp], [vport_stt], [vport_vxlan]],
              [modprobe -q mod || echo "Module mod not loaded."
               on_exit 'modprobe -q -r mod'
              ])
   on_exit 'ovs-dpctl del-dp ovs-system'
   on_exit 'ovs-appctl dpctl/flush-conntrack'
   _OVS_VSWITCHD_START([])
   dnl Add bridges, ports, etc.
   AT_CHECK([ovs-vsctl -- _ADD_BR([br0]) -- $1 m4_if([$2], [], [], [| uuidfilt])], [0], [$2])
])

# OVS_TRAFFIC_VSWITCHD_STOP([WHITELIST], [extra_cmds])
#
# Gracefully stops ovs-vswitchd and ovsdb-server, checking their log files
# for messages with severity WARN or higher and signaling an error if any
# is present.  The optional WHITELIST may contain shell-quoted "sed"
# commands to delete any warnings that are actually expected, e.g.:
#
#   OVS_TRAFFIC_VSWITCHD_STOP(["/expected error/d"])
#
# 'extra_cmds' are shell commands to be executed afte OVS_VSWITCHD_STOP() is
# invoked. They can be used to perform additional cleanups such as name space
# removal.
m4_define([OVS_TRAFFIC_VSWITCHD_STOP],
  [OVS_VSWITCHD_STOP([$1])
   AT_CHECK([:; $2])
  ])

# CONFIGURE_VETH_OFFLOADS([VETH])
#
# The kernel datapath has no problem with offloads and veths. Nothing
# to do here.
m4_define([CONFIGURE_VETH_OFFLOADS],
)

# CHECK_CONNTRACK()
#
# Perform requirements checks for running conntrack tests, and flush the
# kernel conntrack tables when the test is finished.
#
m4_define([CHECK_CONNTRACK],
    [AT_SKIP_IF([test $HAVE_PYTHON = no])
     m4_foreach([mod], [[nf_conntrack_ipv4], [nf_conntrack_ipv6], [nf_nat_ftp],
                        [nf_nat_tftp]],
                [modprobe mod || echo "Module mod not loaded."
                 on_exit 'modprobe -r mod'
                ])
     sysctl -w net.netfilter.nf_conntrack_helper=0
     on_exit 'ovstest test-netlink-conntrack flush'
    ]
)

# CHECK_CONNTRACK_ALG()
#
# Perform requirements checks for running conntrack ALG tests. The kernel
# supports ALG, so no check is needed.
#
m4_define([CHECK_CONNTRACK_ALG])

# CHECK_CONNTRACK_FRAG()
#
# Perform requirements checks for running conntrack fragmentations tests.
# The kernel always supports fragmentation, so no check is needed.
m4_define([CHECK_CONNTRACK_FRAG])

# CHECK_CONNTRACK_LOCAL_STACK()
#
# Perform requirements checks for running conntrack tests with local stack.
# The kernel always supports reading the connection state of an skb coming
# from an internal port, without an explicit ct() action, so no check is
# needed.
m4_define([CHECK_CONNTRACK_LOCAL_STACK])

# CHECK_CONNTRACK_NAT()
#
# Perform requirements checks for running conntrack NAT tests. The kernel
# always supports NAT, so no check is needed.
#
m4_define([CHECK_CONNTRACK_NAT])

# CHECK_CT_DPIF_SET_GET_MAXCONNS()
#
# Perform requirements checks for running ovs-dpctl ct-set-maxconns or
# ovs-dpctl ct-get-maxconns. The kernel datapath does not support this
# feature.
m4_define([CHECK_CT_DPIF_SET_GET_MAXCONNS],
[
    AT_SKIP_IF([:])
])

# CHECK_CT_DPIF_GET_NCONNS()
#
# Perform requirements checks for running ovs-dpctl ct-get-nconns. The
# kernel datapath does not support this feature.
m4_define([CHECK_CT_DPIF_GET_NCONNS],
[
    AT_SKIP_IF([:])
])
